/* --------------------- Rexx ------------------------------ *
 *                                                           *
 *         $$$$  CCCC   A   TTTTT DDDD  EEEEE FFFFF          *
 *        $ $   C      A A    T   D   D E     F              *
 *         $$$  C     A   A   T   D   D EEEE  FFFF           *
 *          $ $ C     AAAAA   T   D   D E     F              *
 *        $$$$   CCCC A   A   T   DDDD  EEEEE F              *
 *                                                           *
 * This exec macro creates the IDCAMS delete and define      *
 * stmts for a list of datasets.  Parm exepected is Catalog  *
 * name, volser and Optional 'O' for (only DEFINE, and no    *
 * DELETE statements).                                       *
 *                                                           *
 * Edit macro reads the currently edited file, each line     *
 * contains a Data set name, and generates the statements    *
 * to DELETE and DEFINE the data set specifying catalog name *
 * and Volume (supplied as part of the input parm). Of course*
 * if 'O' was specified, then Only 'DEFINE' statements will  *
 * get created.                                              *
 *                                                           *
 * Example:                                                  *
 *                                                           *
 *    $CATDEF CATALOG.MASTER.CSYS.VCATCCC &SYSR6             *
 *       (Both DELETE and DEFINE statements created)         *
 *                                                           *
 *    $CATDEF CATALOG.MASTER.CSYS.VCATCCC &SYSR6 O           *
 *       (Only DEFINE statements created)                    *
 *                                                           *
 * Chg - 09/26/2018 - VNV - Created first time               *
 *                                                           *
 * --------------------------------------------------------- */

parse source . . macro_Name .

if sysvar(SYSISPF) ¬="ACTIVE" then do
   parse source . . exec_Name .
   say macro_Name "can only run under ISPF."
   exit
   end

address ISPEXEC

"CONTROL ERRORS RETURN"
"ISREDIT MACRO (PARMS)"
If rc ¬=0 then do
   zedsmsg = macro_Name "error"
   zedlmsg = macro_Name "can only be run as an ispf edit macro !!!"
   "SETMSG MSG(ISRZ001)"
   exit 12
   end

UPPER parms
parse var parms $cat $vol $opt .

if $cat = "" | $vol = "" then do
   ZEDSMSG = ""
   ZEDLMSG = "Catalog_name and/or Volume not specified... Abort.",
             "Syntax required:" macro_Name "Catalog_Name Volume xxx.",
             "xxx is Optional, has to be 'O' and only specified when",
             "the DELETE statements do not need to be created."
   'SETMSG MSG(ISRZ001)'
   exit 12
   end

"ISREDIT (STRTLINE) = LINENUM .ZFIRST"
"ISREDIT (ENDLINE)  = LINENUM .ZLAST"
"ISREDIT RESET"

inData. = ""

do i=strtLine to endLine
   "ISREDIT (TEMP) = LINE" i
   inData.i = STRIP(temp)
   end

Count = i-1

"ISREDIT DELETE NX ALL"

j=0
do i=strtLine to endLine
   if indata.i = "" | SUBSTR(indata.i,1,1) = "*" then do
      count = count-1
      if SUBSTR(indata.i,1,1) = "*" then do
         j=j+1;out.j  = indata.i
         end
      iterate
      end
   indata.i = TRANSLATE(indata.i)
   if $opt = "O" then nop    /* don't create DELETE statements     */
   else do
      j=j+1;out.j  = "                                              "
      j=j+1;out.j  = "  DELETE ("||LEFT(indata.i,44)||") -          "
      j=j+1;out.j  = "    NONVSAM NOSCRATCH -                       "
      j=j+1;out.j  = "    CAT("||STRIP($cat)||")                    "
      j=j+1;out.j  = "  IF LASTCC = 8 THEN SET MAXCC = 0            "
      end
   j=j+1;out.j  = "                                              "
   j=j+1;out.j  = "  DEFINE NVSAM(NAME("||LEFT(indata.i,44)") -  "
   j=j+1;out.j  = "    DEVT(00000000) VOL("||STRIP($vol)||"))  - "
   j=j+1;out.j  = "    CAT("||STRIP($cat)||")                    "
   end

do i=j to 1 by -1
   tline = out.i
   "ISREDIT LINE_AFTER" 0 "= (TLINE)"
   end

zedsmsg = count "Datasets found"
zedlmsg = count "Datasets were found in the control cards, and",
            "the appropriate IDCAMS statements were created for",
            "catalog" $cat "on volume" $vol"."
"SETMSG MSG(ISRZ000)"

exit

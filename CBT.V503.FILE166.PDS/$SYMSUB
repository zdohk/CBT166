/* ----------------------- rexx ------------------------------- *
 * This Edit macro will take the data being edited and replace  *
 * the system symbolics with what's their values found in the   *
 * system.  External Rexx routine $RXSYMBL is called to         *
 * resolve/substitute the symbolics.                            *
 *                                                              *
 * Chg - VNV - 11/10/04 -  Added the first time.                *
 *       VNV - 01/24/01 -  Change to not update the member      *
 *                         unless UP(DATE) is used as parm when *
 *                         edit macro invoked.  Otherwise, new  *
 *                         data will appear as part of the      *
 *                         NOTELINE only (no update occurred).  *
 *                                                              *
 * ------------------------------------------------------------ */

parse source . . macro_Name .

if sysvar(SYSISPF) ¬='ACTIVE' then do
   parse source . . exec_Name .
   say macro_Name 'can only run under ISPF.'
   exit
   end

address ISPEXEC

'CONTROL ERRORS RETURN'
'ISREDIT MACRO (UPDATE)'
If rc ¬=0 then do
   zedsmsg = macro_Name 'error'
   zedlmsg = macro_Name 'can only be run as an ispf edit macro !!!'
   'SETMSG MSG(ISRZ001)'
   exit
   end

upper update

if ABBREV(update,'UP') > 0 then updateIt = 1
else updateIt = 0

'ISREDIT (STRTLINE) = LINENUM .ZFIRST'
'ISREDIT (ENDLINE) = LINENUM .ZLAST'
'ISREDIT RESET'

ct = 0

do i=strtLine to endLine
   'ISREDIT (TEMP) = LINE' i
   newData = $RXSYMBL(LEFT(temp,72))
   if newData ¬== LEFT(temp,72) then do
      ct = ct + 1
      if updateIt then do
         'ISREDIT LINE' i '= (NEWDATA)'
         'ISREDIT LINE_BEFORE' i '= NOTELINE (TEMP)'
         end
      else do
         'ISREDIT LINE_AFTER' i '= NOTELINE (newData)'
         end
      end
   end

zedsmsg = ct 'of' endLine-strtLine 'lines changed.'
zedlmsg = 'Total number of lines edited:' endLine-strtLine'.',
          'Number of lines changed due to symbolic substitution:' ct,
          '(if UPD was specified, otherwise, the data is unchanged). ',
          'There is an NOTELINE above each line that was changed. To',
          'find these lines, user LOC SPECIAL and/or LOC SPECIAL NEXT.'
'SETMSG MSG(ISRZ000)'

exit

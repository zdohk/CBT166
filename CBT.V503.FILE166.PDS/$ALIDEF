/* --------------------- Rexx ------------------------------ *
 *                                                           *
 *         $$$$   A   L      III  DDDD  EEEEE FFFFF          *
 *        $ $    A A  L       I   D   D E     F              *
 *         $$$  A   A L       I   D   D EEEE  FFFF           *
 *          $ $ AAAAA L       I   D   D E     F              *
 *        $$$$  A   A LLLLL  III  DDDD  EEEEE F              *
 *                                                           *
 * This exec macro creates the IDCAMS delete and define      *
 * alias for a list of datasets.  Parm exepected is Catalog  *
 * name, and Optional 'O' for (only DEFINE, and no DELETE    *
 * statements for the alias before re-defining it).          *
 *                                                           *
 * Edit macro reads the currently edited file, each line     *
 * contains the alias and its data set name, creates the     *
 * DELETE and re-defines the alias to point its associated   *
 * data set (unless optional parm 'O' was supplied, in which *
 * case, no DELETE statements get created).                  *
 *                                                           *
 * Example:                                                  *
 *                                                           *
 *    $ALIDEF CATALOG.MASTER.CSYS.VCATCCC                    *
 *       (Both DELETE and DEFINE statements created)         *
 *                                                           *
 *    $ALIDEF CATALOG.MASTER.CSYS.VCATCCC O                  *
 *       (Only DEFINE statements created)                    *
 *                                                           *
 * Control card format:                                      *
 *                                                           *
 *    alias_name True_dataset_name                           *
 *                                                           *
 * Chg - 09/27/2018 - VNV - Created first time               *
 *                                                           *
 * --------------------------------------------------------- */

parse source . . macro_Name .

if sysvar(SYSISPF) ¬="ACTIVE" then do
   parse source . . exec_Name .
   say macro_Name "can only run under ISPF."
   exit
   end

address ISPEXEC

"CONTROL ERRORS RETURN"
"ISREDIT MACRO (PARMS)"
If rc ¬=0 then do
   zedsmsg = macro_Name "error"
   zedlmsg = macro_Name "can only be run as an ispf edit macro !!!"
   "SETMSG MSG(ISRZ001)"
   exit 12
   end

UPPER parms
parse var parms $cat $opt .

if $cat = "" | $vol = "" then do
   ZEDSMSG = ""
   ZEDLMSG = "Catalog_name not specified... Abort.",
             "Syntax required:" macro_Name "Catalog_Name xxx.",
             "xxx is Optional, has to be 'O' and only specified when",
             "the DELETE statements do not need to be created."
   'SETMSG MSG(ISRZ001)'
   exit 12
   end

"ISREDIT (STRTLINE) = LINENUM .ZFIRST"
"ISREDIT (ENDLINE)  = LINENUM .ZLAST"
"ISREDIT RESET"

inData. = ""

do i=strtLine to endLine
   "ISREDIT (TEMP) = LINE" i
   inData.i = STRIP(temp)
   end

Count = i-1

"ISREDIT DELETE NX ALL"

j=0
do i=strtLine to endLine
   if indata.i = "" | SUBSTR(indata.i,1,1) = "*" then do
      count = count-1
      if SUBSTR(indata.i,1,1) = "*" then do
         j=j+1;out.j  = indata.i
         end
      iterate
      end
   indata.i = TRANSLATE(indata.i)
   parse var indata.i $alias $truDsn .
   if $opt = "O" then nop    /* don't create DELETE statements     */
   else do
      j=j+1;out.j  = "                                              "
      j=j+1;out.j  = "  DELETE ("||STRIP($alias)||")   ALIAS -      "
      j=j+1;out.j  = "    CATALOG("||STRIP($cat)||")                "
      j=j+1;out.j  = "  IF LASTCC = 8 THEN SET MAXCC = 0            "
      end
   j=j+1;out.j  = "                                              "
   j=j+1;out.j  = "  DEFINE ALIAS  (NAME("||STRIP($alias)") -   "
   j=j+1;out.j  = "    RELATE("||STRIP($trudsn)")) -            "
   j=j+1;out.j  = "    CATALOG("||STRIP($cat)||")               "
   end

do i=j to 1 by -1
   tline = out.i
   "ISREDIT LINE_AFTER" 0 "= (TLINE)"
   end

zedsmsg = count "Datasets found"
zedlmsg = count "Datasets were found in the control cards, and",
            "the appropriate IDCAMS statements were created for",
            "catalog" $cat"."
"SETMSG MSG(ISRZ000)"

exit
